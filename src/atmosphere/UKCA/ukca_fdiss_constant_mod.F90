! *****************************COPYRIGHT*******************************
! (C) Crown copyright Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
! *****************************COPYRIGHT*******************************
!
!  Description:
!   Module to contain subroutine to calculate the fraction of each
!   species in each dissolved state.  Part of the offline oxidants
!   scheme where ozone is read in from an offline file and hence
!   cannot be washed out.  However the SO2 oxidation scheme requires
!   ozone as an aqueous-phase oxidant.
!
!  UKCA is a community model supported by The Met Office and
!  NCAS, with components initially provided by The University of
!  Cambridge, University of Leeds, University of Oxford, and
!  The Met Office. See www.ukca.ac.uk
!
! Code Owner: Please refer to the UM file CodeOwners.txt
! This file belongs in section: UKCA
!
!  Code Description:
!    Language:  FORTRAN 95
!    This code is written to UMDP3 programming standards.
!
! ######################################################################
!
MODULE ukca_fdiss_constant_mod

IMPLICIT NONE

CHARACTER(LEN=*), PARAMETER, PRIVATE :: ModuleName='UKCA_FDISS_CONSTANT_MOD'

CONTAINS

! ######################################################################

SUBROUTINE ukca_fdiss_constant(n_points, qcl_min, t, p, qcl,                   &
  fdiss_constant)


USE asad_mod,            ONLY: ct_ddhr, ct_dhr, ct_kd298, ct_k298,             &
  jpeq
USE ukca_chem_offline,   ONLY: nwet_constant
USE ukca_constants,      ONLY: avogadro, rmol, H_plus, m_air
USE parkind1,            ONLY: jprb, jpim
USE yomhook,             ONLY: lhook, dr_hook
USE Control_Max_Sizes
IMPLICIT NONE

INTEGER, INTENT(IN) :: n_points         ! No of points

REAL, INTENT(IN) :: qcl_min             ! do calcs when qcl > qcl_min
REAL, INTENT(IN) :: t(n_points)         ! Temperature (K)
REAL, INTENT(IN) :: p(n_points)         ! Pressure (Pa)
REAL, INTENT(IN) :: qcl(n_points)       ! Cloud liquid water (kg/kg)

! Fraction dissociated
REAL, INTENT(OUT) :: fdiss_constant(n_points, nwet_constant, jpeq+1)

! Local variables
INTEGER :: ns                   ! Loop variable

REAL, PARAMETER :: p0      = 1.0135e5   ! P0 in Pa
REAL, PARAMETER :: inv_298 = 1.0/298.0  ! 1/298

REAL :: tmp1(n_points)                  ! Temporary variable
REAL :: tmp2(n_points)                  ! Temporary variable
REAL :: Henry(n_points)                 ! Henry's Law consts
REAL :: Henry_eff(n_points)             ! Effective  ---"---
REAL :: qcla(n_points)                  ! qcl in kg/m^3
LOGICAL :: todo(n_points)               ! defines qcl > qcl_min

! Aqueous equilibrium constants:
REAL :: eq_con(n_points,jpeq)

INTEGER(KIND=jpim), PARAMETER :: zhook_in  = 0
INTEGER(KIND=jpim), PARAMETER :: zhook_out = 1
REAL(KIND=jprb)               :: zhook_handle

CHARACTER(LEN=*), PARAMETER :: RoutineName='UKCA_FDISS_CONSTANT'

IF (lhook) CALL dr_hook(ModuleName//':'//RoutineName,zhook_in,zhook_handle)


fdiss_constant(:,:,:) = 0.0
todo(:) = .FALSE.
WHERE (qcl(:) > qcl_min)
  todo(:) = .TRUE.
  qcla(:) = qcl(:)*M_air*p(:)/(Rmol*t(:))
  tmp1(:) = (1.0/t(:)) - inv_298
  tmp2(:) = Rmol*qcla(:)*t(:)
END WHERE

DO ns=1,nwet_constant
  IF (ct_kd298(ns,2) > 1e-12) THEN      ! 2nd dissociation constant
    WHERE (todo(:))
      Henry(:) = ct_k298(ns) * EXP(ct_dhr(ns)*tmp1(:))
      eq_con(:,1) = ct_kd298(ns,1) * EXP(ct_ddhr(ns,1)*tmp1(:))
      eq_con(:,2) = ct_kd298(ns,2) * EXP(ct_ddhr(ns,2)*tmp1(:))
      Henry_eff(:) = Henry(:)*(1.0+eq_con(:,1)/H_plus)
      fdiss_constant(:,ns,1) = 1.0/(1.0 + (p0/(tmp2(:)*Henry(:))))
      fdiss_constant(:,ns,2) = 1.0/(1.0 + (p0/(tmp2(:)*                        &
        Henry_eff(:))))
      fdiss_constant(:,ns,2) = fdiss_constant(:,ns,2) -                        &
        fdiss_constant(:,ns,1)
      Henry_eff(:) = Henry(:) * (1.0 + eq_con(:,1)/                            &
        H_plus + eq_con(:,1)*eq_con(:,2)/H_plus**2)
      fdiss_constant(:,ns,3) = 1.0/(1.0 + (p0/(tmp2(:)*                        &
        Henry_eff(:))))
      fdiss_constant(:,ns,3) = fdiss_constant(:,ns,3) -                        &
        fdiss_constant(:,ns,2) -                                               &
        fdiss_constant(:,ns,1)
    END WHERE
  ELSE IF (ct_kd298(ns,1) > 1e-12) THEN  ! one dissociation
    WHERE (todo(:))
      Henry(:) = ct_k298(ns) * EXP(ct_dhr(ns)*tmp1(:))
      eq_con(:,1) = ct_kd298(ns,1) * EXP(ct_ddhr(ns,1)*tmp1(:))
      Henry_eff(:)=Henry(:)*(1.0+eq_con(:,1)/H_plus)
      tmp2(:) = Rmol*qcla(:)*t(:)
      fdiss_constant(:,ns,1) = 1.0/(1.0 + (p0/(tmp2(:)*                        &
        Henry(:))))
      fdiss_constant(:,ns,2) = 1.0/(1.0 + (p0/(tmp2(:)*                        &
        Henry_eff(:))))
      fdiss_constant(:,ns,2) = fdiss_constant(:,ns,2) -                        &
        fdiss_constant(:,ns,1)
    END WHERE
  ELSE                                  ! No dissociation
    IF (ct_k298(ns) > 1e-12) THEN          ! avoid glitch with BrONO2
      WHERE (todo(:))
        Henry(:) = ct_k298(ns) * EXP(ct_dhr(ns)*tmp1(:))
        fdiss_constant(:,ns,1) = 1.0/(1.0 + (p0/(tmp2(:)*Henry(:))))
      END WHERE
    END IF
  END IF
END DO

IF (lhook) CALL dr_hook(ModuleName//':'//RoutineName,zhook_out,zhook_handle)
RETURN
END SUBROUTINE ukca_fdiss_constant

END MODULE ukca_fdiss_constant_mod
